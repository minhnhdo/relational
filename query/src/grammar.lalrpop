use crate::ast::CompoundOperator;
use crate::ast::CompoundedSelectCores;
use crate::ast::Expression;
use crate::ast::Literal;
use crate::ast::ResultColumn;
use crate::ast::SelectCore;
use crate::ast::SelectStatement;
use crate::ast::SelectStatementCore;
use crate::ast::string::parse_sql_string;
use lalrpop_util::ParseError;

grammar;

SingleString: String = {
    <s:r"'(''|[^'])*'"> =>? parse_sql_string(s)
        .map_err(|e| ParseError::User {
            error: e.message(),
        }),
}

pub String: Literal = {
    <s:SingleString+> => Literal::String(s.into_iter().collect()),
}

Literal: Literal = {
    <s:String> => s,
}

Expression: Expression = {
    <l:Literal> => Expression::Literal(l),
}

SingleResultColumn: ResultColumn = {
    <e:Expression> => ResultColumn::Expression(e),
}

ResultColumns: Vec<ResultColumn> = {
    <mut init:(<SingleResultColumn> ",")*> <last:SingleResultColumn> => {
        init.push(last);
        init
    }
}

SelectCore: SelectCore = {
    r"(?i)select" <result_columns:ResultColumns> => SelectCore { result_columns },
}

CompoundOperator: CompoundOperator = {
    r"(?i)union" => CompoundOperator::Union,
    r"(?i)union" r"(?i)all" => CompoundOperator::UnionAll,
    r"(?i)intersect" => CompoundOperator::Intersect,
    r"(?i)except" => CompoundOperator::Except,
}

CompoundedSelectCores: CompoundedSelectCores = {
    <lhs:SelectCore> <operator:CompoundOperator> <rhs:SelectCore> => CompoundedSelectCores {
        lhs: SelectStatementCore::Simple(Box::new(lhs)),
        operator,
        rhs,
    },
    <lhs:CompoundedSelectCores> <operator:CompoundOperator> <rhs:SelectCore> => CompoundedSelectCores {
        lhs: SelectStatementCore::Compounded(Box::new(lhs)),
        operator,
        rhs,
    },
}

pub SelectStatement: SelectStatement = {
    <c:SelectCore> => SelectStatement { core: SelectStatementCore::Simple(Box::new(c)) },
    <c:CompoundedSelectCores> => SelectStatement { core: SelectStatementCore::Compounded(Box::new(c)) },
}
